namespace sample;

use com.ibm.streamsx.mongodb::*;

/**
* Run two beacons to create different inputs.
* Look inside [HelloWorldInsertSample] SPL code to see different uses of InsertDocument with the description.
*/
composite HelloWorldInsertSample {
	graph
		stream<rstring greeting1> B1Stream = Beacon()  {
            param iterations: 1;
            output B1Stream :
            	greeting1 = "Hello world";
        }
        
		stream<rstring greeting2> B2Stream = Beacon()  {
            param iterations: 1;
            output B2Stream :
            	greeting2 = "Hello again";
        }
        
        stream<rstring error1, rstring error2, rstring error3, rstring error4, rstring error5, rstring error6, rstring error7, rstring error8> MongoSink =
        	Insert(B1Stream; B2Stream) {
        	param
        		dbHost : "localhost";
        		
        	/*
        	 * Uncomment authentication parameter for custom authentication method
        	 */

//        		authentication : {mechanism = getSubmissionTimeValue("mechanism", "SCRAM-SHA-1"),
//        						  user = getSubmissionTimeValue("username", "mongouser"),
//        						  pwd = getSubmissionTimeValue("password", "passw0rd"),
//        						  db = getSubmissionTimeValue("adminDB", "admin"),
//        						  digestPassword = (boolean)getSubmissionTimeValue("digestPassword", "true"),
//        						  serviceName = getSubmissionTimeValue("serviceName", ""),
//        						  serviceHostname = getSubmissionTimeValue("serviceHostname", "")};


        	/*
        	 * Uncomment sslOptions parameter to configure SSL
        	 */
				// Create etc directory and add there PEM file.
//        		sslOptions : {sslMode = true, caFile = "", pemKeyFile = getThisToolkitDir() + "/etc/mongodb.pem", pemKeyPassword = "", crlFile = "", allowInvalidCertificates = false, allowInvalidHostnames = false};

        	/*
        	 * Uncomment username/password parameters for a default authentication method
        	 */
//        		username : getSubmissionTimeValue("username", "mongouser");
//        		password : getSubmissionTimeValue("password", "passw0rd");

        	/*
        	 * Uncomment profiling parameter to enable profiling mode
        	 */
//        		profiling : all;

        	output MongoSink :
        		/**
				 * Store a tuple as is, the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c80f"), "greeting1" : "Hello world" }
				 */
        		error1 = InsertDocument("streams", "sample", B1Stream),

        		/**
				 * Store a tuple under the specified 'greetings2' key, the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c810"), "greeting2" : { "greeting2" : "Hello again" } }
				 */
        		error2 = InsertDocument("streams", "sample", "greeting2", B2Stream),

        		/**
				 * Store a tuple attribute under the specified 'greetings3' key (simple type always requires a key), the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c811"), "greeting3" : "Hello world" }
				 */
        		error3 = InsertDocument("streams", "sample", "greeting3", greeting1),

        		/**
				 * Store a constant list under the specified 'greetings4' key (list/set type always requires a key), the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c812"), "greeting4" : [  "Hello",  "world" ] }
				 */
        		error4 = InsertDocument("streams", "sample", "greeting4", ["Hello", "world"]),

        		/**
				 * Store a constant map (map/tuple doesn't require key, but can use it to add a root key), the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c813"), "greeting5_1" : "Hello", "greeting5_2" : "world" }
				 */
        		error5 = InsertDocument("streams", "sample", {"greeting5_1" : "Hello", "greeting5_2" : "world"}),

        		/**
				 * Store a constant rstring JSON, the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c814"), "greeting6_1" : "Hello", "greeting6_2" : "world" }
				 */
        		error6 = InsertDocumentAsJson("streams", "sample", '{"greeting6_1" : "Hello", "greeting6_2" : "world"}'),

        		/**
				 * Store a constant rstring JSON (doesn't require key, but can use it to add a root key), the resulting document is:
				 * { "_id" : ObjectId("53c795f7e66ebea0c454c815"), "greeting7_1" : { "greeting7_2" : [ "Hello", "world" ] } }
				 */
        		error7 = InsertDocumentAsJson("streams", "sample", "greeting7_1", '{"greeting7_2" : ["Hello", "world"]}'),

        		/**
				 * 	Store a constant rstring JSON with wrong syntax, the exception is:
				 * FailedToParse: First character in field must be [A-Za-z$_]: offset:1 of:{[ : "Hello world"}
				 */
        		error8 = InsertDocumentAsJson("streams", "sample", '{[ : "Hello world"}');
        }
        
		/**
		 * Will be called when at least one error occured
		 */
        () as ErrorSink = FileSink(MongoSink)   {
            param
                file : "/dev/stdout";
                flush : 1u;
                quoteStrings : false;
        }
}
